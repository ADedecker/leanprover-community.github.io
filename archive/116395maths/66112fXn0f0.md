---
layout: page
title: Lean Prover Zulip Chat Archive 
permalink: archive/116395maths/66112fXn0f0.html
---

## Stream: [maths](index.html)
### Topic: [f*X^n=0 -> f=0](66112fXn0f0.html)

---

#### [Kevin Buzzard (Sep 09 2018 at 02:47)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133588233):
I've been working with `polynomial.lean`. I would like
```
mul_X_pow_eq_zero :
  ∀ {α : Type u_1} [_inst_1 : decidable_eq α] [_inst_2 : comm_semiring α] (n : ℕ) (f : polynomial α),
    f * X ^ n = 0 → f = 0
```
but I can't see it and it's not so clear to me how to get it from the interface. I am tempted to get my hands dirty and prove `(f * X ^ n) (m) = f (n + m)` [i.e. break the illusion and start treating polynomials as functions from nat to alpha] from which it follows relatively simply, but I suspect this would involve wading into finsupp. @**Chris Hughes** or anyone else -- am I missing a trick here?

#### [Mario Carneiro (Sep 09 2018 at 03:06)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133588722):
Do we have left-regular elements?

#### [Mario Carneiro (Sep 09 2018 at 03:07)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133588727):
i.e. non-zero divisors

#### [Mario Carneiro (Sep 09 2018 at 03:09)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133588774):
I would suggest using integral domain properties but I guess you don't have that in a comm_semiring

#### [Chris Hughes (Sep 09 2018 at 09:33)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133598927):
I think `leading_coeff_mul` and `leading_coeff_X_pow` should help  here,

#### [Kevin Buzzard (Sep 09 2018 at 11:26)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133601842):
The ring could even be the zero ring here! It's a particularly annoying edge case

#### [Kevin Buzzard (Sep 09 2018 at 11:34)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133602100):
Unfortunately that's how I got here -- I am trying to prove that the leading coefficient of `f*X^n` equals the leading coefficient of `f`and this is a goal that pops up along the way

#### [Chris Hughes (Sep 09 2018 at 12:15)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133603221):
I just generalized `leading_coeff_X_pow` to a `comm_semiring` which should help with the lemma about `leading_coeff (f * X ^ n)`.

#### [Kevin Buzzard (Sep 09 2018 at 12:44)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133604218):
I'd love to be proved wrong, but I don't think this is the right direction. The problem I have is not with `leading_coeff_X_pow`, it's with `leading_coeff_mul` and friends. The `mul` version needs an integral domain so I can't use it (I need a result over a general `comm_ring`). The `mul'` version cannot say "if the leading coefficients of `f` and `g` are non-zero then the leading coefficient of `fg` is the product" because this is not true (if the product of the leading coefficients is zero then there's a problem), so it says "if the leading coefficient of `f*g` is non-zero then...". Because the leading coefficient of a polynomial is non-zero iff the polynomial is non-zero I need *exactly* that if `f` is non-zero then `f*X^n` is non-zero to apply this lemma. This was what led me to the question in the title. My instinct is that one should prove this by hand. I can't seem to find `coefficient f d`, the coefficient of $$X^d$$ in $$f$$. Of course it's just `f d` but I think it needs some notation because it's a fundamental concept. With this notion (which I could PR no problem) we have `f = 0` iff all its coefficients are zero (no problem), and then what I want is `coefficient (f * X) (d + 1) = coefficient f d`, for which I am going to have to go beyond the interface and into the definition of multiplication of polynomials, and I guess this is the explict lemma that I am asking for advice on. Once I have this I I think I can do everything else.

#### [Chris Hughes (Sep 09 2018 at 12:51)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133604373):
For `leading_coeff (f * X ^ n)` I would case on whether `f = 0`, if `f = 0` it's trivial, otherwise it's `leading_coeff_mul'` and you have to prove that `leading_coeff f * 1 != 0` which is easy because `f != 0` and `leading_coeff f = 0 iff f = 0`

#### [Reid Barton (Sep 09 2018 at 12:53)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133604414):
That relation `coefficient (f * X) (d + 1) = coefficient f d` seems basic and useful enough that it should be provided as a lemma in any case.

#### [Reid Barton (Sep 09 2018 at 12:53)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133604419):
Or the same for `f * X^n`.

#### [Chris Hughes (Sep 09 2018 at 12:53)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133604423):
Johannes was talking about some way of representing polynomial as `C a + X * (C b + X * (...))`, which might give some more general strategies for dealing with this, because I do agree it is annoying at the moment.

#### [Chris Hughes (Sep 09 2018 at 12:55)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133604472):
It might just be a case of people getting stuck and proving millions of lemmas that I didn't think of like `(f * X) (d + 1) = f d` until the interface is good.

#### [Kevin Buzzard (Sep 09 2018 at 12:57)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133604521):
@**Chris Hughes** got it -- thanks.

#### [Kenny Lau (Sep 09 2018 at 17:48)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133615540):
```lean
import data.polynomial

universe u
variables {α : Type u} [decidable_eq α] [comm_semiring α]
variables (n : ℕ) (f : polynomial α)

theorem mul_X_pow_eq_zero (H : f * polynomial.X ^ n = 0) : f = 0 :=
begin
  ext i,
  have H1 : (f * polynomial.X^n) (i+n) = 0, {rw H, refl},
  rw polynomial.zero_apply,
  by_contra H2,
  have H3 : (@polynomial.X α _ _)^n = finsupp.single n (1:α),
  { have H3 := @polynomial.single_eq_C_mul_X α 1 _ _ n,
    simp [H3], symmetry, exact one_mul _ },
  have H4 : finset.sum (finset.singleton i) _ = (f * polynomial.X^n) (i+n),
  { rw [finsupp.mul_def, finsupp.sum_apply, finsupp.sum],
    apply finset.sum_subset,
    { intro j, simp [H2] {contextual:=tt} },
    intros k h1 h2,
    simp at h2,
    rw [finsupp.sum_apply, H3, finsupp.sum_single_index];
    rw finsupp.single_eq_of_ne (mt nat.add_right_cancel h2) },
  rw [H1, finset.sum_singleton, finsupp.sum_apply, H3] at H4,
  rw [finsupp.sum_single_index, finsupp.single_apply, if_pos rfl, mul_one] at H4,
  exact H2 H4,
  simp [finsupp.single_apply, mul_zero]
end
```

#### [Kevin Buzzard (Sep 10 2018 at 21:37)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133683986):
So Kenny has proved the result in perhaps the "correct" way. But I am confused about these things more generally. I talked to Chris about these things today and he cannot see the point of adding `def : coeff f n := f n` because "it is there already". I don't like `f n` at all for the coefficient of $$X^n$$ in $$f$$ because it looks misleading (it looks like evaluation) and ultimately people will need theorems such as `coeff (f * X ^ n) (i + n) = coeff f i`. Now I guess one could argue that one could still put such theorems in and give them appropriate names, but just write them as `(f * X ^ n) (i + n) = f i`. I do trust Chris in general but on the other hand I feel like making a good interface is important. Does anyone have any input on this? I feel it's a bit like making `set X = X -> Prop` and then arguing that you don't need `mem` because "it is there already".

#### [Johan Commelin (Sep 10 2018 at 21:38)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133684055):
I completely agree with you that `f n` looks very ~~evil~~eval.

#### [Chris Hughes (Sep 10 2018 at 21:45)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133684380):
Maybe we should change the `has_coe_to_fun` to evaluation, and use `coeff` for coefficients.

#### [Johan Commelin (Sep 10 2018 at 21:46)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133684430):
In fact, I have even wondered if we could set up some coercion to turn `f x` into the evaluation of `f` at `x`. But maybe that is also very confusing. I don't know.

#### [Johan Commelin (Sep 10 2018 at 21:46)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133684444):
In general I wouldn't mind having `coeff n f` for the `n`th coefficient of `f`.

#### [Johan Commelin (Sep 10 2018 at 21:48)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133684551):
I agree that it is part of my "mental" interface to polynomials. After all "polynomials" are just some gadgets that float around. They aren't functions on `nat`. But you can somehow access their coefficients.

#### [Kevin Buzzard (Sep 10 2018 at 21:48)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133684552):
Kenny can you prove

```lean
import data.polynomial

universe u
variables {α : Type u} [decidable_eq α] [comm_semiring α]
variables (n : ℕ) (f : polynomial α)

theorem mul_X_coeff : (f * polynomial.X) (n + 1) = f n := sorry
```

in a similar way?

#### [Kenny Lau (Sep 10 2018 at 21:50)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133684651):
sure

#### [Kenny Lau (Sep 10 2018 at 21:50)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133684657):
in fact that's what I did at first

#### [Kenny Lau (Sep 10 2018 at 21:50)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133684658):
and then I realized that I can generalize it

#### [Kenny Lau (Sep 10 2018 at 21:50)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133684662):
and prove your theorem directly

#### [Mario Carneiro (Sep 10 2018 at 21:54)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133684862):
I'm okay with using `f x` for evaluation instead of coefficient extraction, but in my experience `eval` is less useful than I would have expected. Usually you actually need `eval2` since the polynomial actually lives in a different scalar ring than the one you are evaluating into

#### [Mario Carneiro (Sep 10 2018 at 21:54)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133684881):
i.e. talking about the complex roots of an integer polynomial

#### [Reid Barton (Sep 10 2018 at 21:56)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133684993):
Is there a reason not to make the coercion use `eval2` then?

#### [Johan Commelin (Sep 10 2018 at 21:57)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133684996):
That's a good point. In practice there will be a canonical ring hom floating around that you want to plug into `eval2`. But I guess this is hard to put into Lean.

#### [Reid Barton (Sep 10 2018 at 21:57)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133685003):
oh, you need the ring hom, I see

#### [Reid Barton (Sep 10 2018 at 21:57)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133685007):
right

#### [Reid Barton (Sep 10 2018 at 21:57)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133685024):
well, if the target ring is an algebra over the ring of definition of the polynomial...

#### [Reid Barton (Sep 10 2018 at 21:58)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133685065):
Excuse me, semialgebra

#### [Reid Barton (Sep 10 2018 at 21:59)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133685128):
I'm thinking of things like P is a polynomial with integer coefficients, and then in Z[X, Y] I want to state that X - Y divides P(X) - P(Y)

#### [Kenny Lau (Sep 10 2018 at 22:01)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133685267):
https://leanprover.zulipchat.com/#narrow/stream/116395-maths/subject/Perfectoid.20spaces/near/133304087

#### [Kenny Lau (Sep 10 2018 at 22:01)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133685272):
[sounds like something I wrote](https://github.com/kckennylau/Lean/blob/master/polynomial.lean)

#### [Reid Barton (Sep 10 2018 at 22:06)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133685601):
`eval' R A x f` is perhaps not quite as beautiful as the `f x` I had in mind, but otherwise yes exactly this.

#### [Reid Barton (Sep 10 2018 at 22:07)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133685627):
Were there class inference problems with `R` and `A`?

#### [Kenny Lau (Sep 10 2018 at 22:21)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133686431):
I forgot

#### [Kenny Lau (Sep 10 2018 at 22:21)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133686435):
I don't know if this will cause troubles

#### [Kevin Buzzard (Sep 10 2018 at 22:30)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133687008):
I don't care about the coercion to fun, I'm just concerned that we don't have a good interface for coefficients. Is there a problem with adding `coeff f n` and using it to state lemmas and making it the "official" interface to access coefficients?

#### [Kenny Lau (Sep 10 2018 at 22:54)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133688234):
```lean
import data.polynomial

universe u
variables {α : Type u} [decidable_eq α] [comm_semiring α]
variables (n : ℕ) (f : polynomial α)

theorem mul_X_coeff : (f * polynomial.X) (n + 1) = f n :=
begin
try_for 400 {
  rw [finsupp.mul_def, finsupp.sum_apply, finsupp.sum, polynomial.X],
  refine (finset.sum_eq_single n _ _).trans _,
  { intros k _ h1,
    rw [finsupp.sum_single_index, finsupp.single_apply],
    rw [if_neg (mt nat.succ_inj h1)],
    rw [mul_zero, finsupp.single_zero] },
  { intro h1,
    rw [finsupp.mem_support_iff, not_not] at h1,
    rw [finsupp.sum_single_index, finsupp.single_apply, if_pos rfl, h1, zero_mul],
    rw [mul_zero, finsupp.single_zero] },
  rw [finsupp.sum_single_index, finsupp.single_apply, if_pos rfl, mul_one],
  rw [mul_zero, finsupp.single_zero]
}
end
```

#### [Kenny Lau (Sep 10 2018 at 22:54)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133688239):
400 ms

#### [Mario Carneiro (Sep 10 2018 at 22:55)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133688313):
by the way, the `try_for` is only for debugging, you don't need to leave it in finished proofs

#### [Kenny Lau (Sep 10 2018 at 22:57)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133688414):
```lean
import data.polynomial

universe u
variables {α : Type u} [decidable_eq α] [comm_semiring α]
variables (n : ℕ) (f : polynomial α)

theorem mul_X_coeff : (f * polynomial.X) (n + 1) = f n :=
begin
try_for 300 {
  rw [finsupp.mul_def, finsupp.sum_apply, finsupp.sum, polynomial.X],
  conv { to_lhs, congr, skip, funext,
    rw [finsupp.sum_single_index, finsupp.single_apply], skip,
    rw [mul_zero, finsupp.single_zero] },
  refine (finset.sum_eq_single n _ _).trans _,
  { intros k _ h1,
    rw [if_neg (mt nat.succ_inj h1)] },
  { intro h1,
    rw [finsupp.mem_support_iff, not_not] at h1,
    rw [if_pos rfl, h1, zero_mul] },
  rw [if_pos rfl, mul_one]
}
end
```

#### [Kenny Lau (Sep 10 2018 at 22:57)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133688422):
300 ms, shorter and faster

#### [Kenny Lau (Sep 10 2018 at 22:57)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133688439):
(remember to remove the `try_for` when you copy)

#### [Kenny Lau (Sep 10 2018 at 23:01)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133688699):
@**Mario Carneiro** is `rw` faster or `exact` faster, in general?

#### [Mario Carneiro (Sep 10 2018 at 23:01)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133688714):
this is a silly question

#### [Mario Carneiro (Sep 10 2018 at 23:01)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133688721):
it depends on a thousand and one factors

#### [Kenny Lau (Sep 10 2018 at 23:02)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133688735):
ok, then let's be specific

#### [Kenny Lau (Sep 10 2018 at 23:02)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133688774):
```lean
import data.polynomial

universe u
variables {α : Type u} [decidable_eq α] [comm_semiring α]
variables (n : ℕ) (f : polynomial α)

theorem mul_X_coeff : (f * polynomial.X) (n + 1) = f n :=
begin
try_for 400 {
  rw [finsupp.mul_def, finsupp.sum_apply, finsupp.sum, polynomial.X],
  conv { to_lhs, congr, skip, funext,
    rw [finsupp.sum_single_index, finsupp.single_apply], skip,
    rw [mul_zero, finsupp.single_zero] },
  refine (finset.sum_eq_single n _ _).trans _,
  { exact λ k _ h1, if_neg (mt nat.succ_inj h1) },
  { exact λ h1, (if_pos rfl).trans ((mul_one _).trans $
      by rwa [finsupp.mem_support_iff, not_not] at h1) },
  exact ((if_pos rfl).trans (mul_one _))
}
end
```

#### [Kenny Lau (Sep 10 2018 at 23:02)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133688787):
why is this slower than the previous one?

#### [Mario Carneiro (Sep 10 2018 at 23:02)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133688793):
the answer is "try it and find out"

#### [Mario Carneiro (Sep 10 2018 at 23:03)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133688824):
there are particular things that can happen that can cause a slowdown, but I don't know if I can account for each millisecond

#### [Kenny Lau (Sep 10 2018 at 23:09)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133689220):
ok seriously it can be as fast as 300 ms as long as I use `rw` instead of `refine`/`exact`

#### [Kenny Lau (Sep 10 2018 at 23:09)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133689223):
```lean
import data.polynomial

universe u
variables {α : Type u} [decidable_eq α] [comm_semiring α]
variables (n : ℕ) (f : polynomial α)

theorem mul_X_coeff : (f * polynomial.X) (n + 1) = f n :=
begin
try_for 300 {
  rw [finsupp.mul_def, finsupp.sum_apply, finsupp.sum, polynomial.X],
  conv { to_lhs, congr, skip, funext,
    rw [finsupp.sum_single_index, finsupp.single_apply], skip,
    rw [mul_zero, finsupp.single_zero] },
  rw [finset.sum_eq_single n, if_pos rfl, mul_one],
  { intros k _ h1,
    rw if_neg (mt nat.succ_inj h1) },
  { intros h1,
    rw [finsupp.mem_support_iff, not_not] at h1,
    rw [if_pos rfl, h1, mul_one] }
}
end
```

#### [Kenny Lau (Sep 10 2018 at 23:09)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133689228):
which is quite counter-intuitive

#### [Mario Carneiro (Sep 10 2018 at 23:11)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133689346):
I would guess the `(if_pos rfl).trans` spots are hard for the elaborator

#### [Kenny Lau (Sep 10 2018 at 23:13)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133689494):
I need to thank the elaborator some time for his hard work

#### [Reid Barton (Sep 11 2018 at 14:19)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133727884):
```quote
I don't care about the coercion to fun, I'm just concerned that we don't have a good interface for coefficients. Is there a problem with adding `coeff f n` and using it to state lemmas and making it the "official" interface to access coefficients?
```
My only real opinion here is there should be exactly one way to write "the nth coefficient of f", so if we add `coeff` (which seems :+1:) then we should remove or change the coercion to fun instance.

#### [Kevin Buzzard (Sep 11 2018 at 14:57)](https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/f%2AX%5En%3D0%20-%3E%20f%3D0/near/133729846):
There are two ways to write "x is an element of the set S" (which has never caused me any problems). Oh, and two ways to write "the integer corresponding to this natural number" (which has).

