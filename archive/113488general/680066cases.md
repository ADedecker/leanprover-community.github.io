---
layout: page
title: Lean Prover Zulip Chat Archive 
permalink: archive/113488general/680066cases.html
---

## [general](index.html)
### [6 cases](680066cases.html)

#### [Kevin Buzzard (Nov 18 2018 at 20:46)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/147928252):
Is there a trick for doing this painlessly:

```lean
lemma H0 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 0 := ⟨4,0,1,rfl⟩
lemma H1 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 1 := ⟨0,5,0,rfl⟩
lemma H2 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 2 := ⟨1,0,2,rfl⟩
lemma H3 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 3 := ⟨0,3,1,rfl⟩
lemma H4 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 4 := ⟨8,0,0,rfl⟩
lemma H5 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 5 := ⟨0,1,2,rfl⟩

example (r : ℕ) (Hr : r < 6) : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + r := sorry
```

Currently I have

```lean
example (r : ℕ) (Hlt6 : r < 6) : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + r :=
begin
  cases (lt_or_eq_of_le (nat.le_of_succ_le_succ Hlt6)) with Hlt5 Heq5,
    cases (lt_or_eq_of_le (nat.le_of_succ_le_succ Hlt5)) with Hlt4 Heq4,
      cases (lt_or_eq_of_le (nat.le_of_succ_le_succ Hlt4)) with Hlt3 Heq3,
        cases (lt_or_eq_of_le (nat.le_of_succ_le_succ Hlt3)) with Hlt2 Heq2,
          cases (lt_or_eq_of_le (nat.le_of_succ_le_succ Hlt2)) with Hlt1 Heq1,
            cases (lt_or_eq_of_le (nat.le_of_succ_le_succ Hlt1)) with Hlt0 Heq0,
              cases Hlt0,
            rw Heq0, exact H0,
          rw Heq1, exact H1,
        rw Heq2, exact H2,
      rw Heq3, exact H3,
    rw Heq4, exact H4,
  rw Heq5, exact H5,
end
```
```

#### [Kevin Buzzard (Nov 18 2018 at 21:00)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/147928746):
Just to be clear -- "this" isn't proving the 6 lemmas, it's proving the example. I guess I'd like some tactic which replaces `r < 6 -> P r` with `P 0 \and P 1 \and P 2 \and P 3 \and P 4 \and P 5` which I could then just kill with `\<H0,H1,H2,H3,H4,H5\>` on one line.

#### [Kevin Buzzard (Nov 18 2018 at 21:02)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/147928810):
Here's the context (question and solution on an M1F example sheet):

https://github.com/ImperialCollegeLondon/M1F_example_sheets/tree/master/src/example_sheet_05/Sht05Q05

#### [Bryan Gin-ge Chen (Nov 18 2018 at 21:07)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/147928998):
This isn't the tactic you want, but is (in my opinion) a little cleaner than what you posted:
```lean
example (r : ℕ) (Hr : r < 6) : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + r :=
match r, Hr with
| 0, H := H0
| 1, H := H1
| 2, H := H2
| 3, H := H3
| 4, H := H4
| 5, H := H5
| (n+6), H := 
  begin
    revert H,
    simp
  end
end
```

#### [Kenny Lau (Nov 18 2018 at 21:16)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/147929396):
```lean
lemma H0 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 0 := ⟨4,0,1,rfl⟩
lemma H1 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 1 := ⟨0,5,0,rfl⟩
lemma H2 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 2 := ⟨1,0,2,rfl⟩
lemma H3 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 3 := ⟨0,3,1,rfl⟩
lemma H4 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 4 := ⟨8,0,0,rfl⟩
lemma H5 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 5 := ⟨0,1,2,rfl⟩

example (r : ℕ) (Hr : r < 6) : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + r :=
begin
  repeat { cases Hr with _ Hr },
  exacts [H5, H4, H3, H2, H1, H0]
end
```

#### [Kevin Buzzard (Nov 18 2018 at 22:43)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/147932349):
Nice! `exacts`??

#### [Kenny Lau (Nov 18 2018 at 22:43)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/147932362):
it clears many goals at once

#### [Kenny Lau (Nov 24 2018 at 02:28)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/148257191):
```lean
import tactic.fin_cases

lemma H0 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 0 := ⟨4,0,1,rfl⟩
lemma H1 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 1 := ⟨0,5,0,rfl⟩
lemma H2 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 2 := ⟨1,0,2,rfl⟩
lemma H3 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 3 := ⟨0,3,1,rfl⟩
lemma H4 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 4 := ⟨8,0,0,rfl⟩
lemma H5 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 5 := ⟨0,1,2,rfl⟩

example (r : ℕ) (Hr : r < 6) : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + r :=
begin
  suffices : ∀ x : fin 6, ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + x.1,
  { exact this ⟨r, Hr⟩ },
  intro x,
  fin_cases x,
  exacts [H0, H1, H2, H3, H4, H5]
end
```

#### [Kenny Lau (Nov 24 2018 at 02:29)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/148257197):
@**Kevin Buzzard**

#### [Kevin Buzzard (Nov 24 2018 at 08:49)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/148267406):
Aah I remember Scott talking about cases on fin n. So this happened? :-) Thanks for pointing it out!

#### [Kenny Lau (Nov 24 2018 at 23:20)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/148293408):
```lean
import tactic.interactive

namespace tactic.interactive
open lean.parser tactic interactive
meta def nat_lt_cases (h : parse ident) : tactic unit :=
do e ← get_local h,
   `(%%val < %%ebound) ← infer_type e,
   bound ← eval_expr ℕ ebound,
   expr.local_const _ nval _ _ ← return val,
   iterate_at_most bound (do val ← get_local nval,
     (_,_,(_,e)::_)::_ ← tactic.cases_core val [nval,h],
     tactic.clear e, swap),
   e ← get_local h, val ← get_local nval,
   proof ← to_expr ```(absurd %%e (not_lt_of_ge $ nat.le_add_left %%ebound %%val)),
   tactic.exact proof,
   goals ← get_goals,
   set_goals goals.reverse,
   skip
end tactic.interactive

lemma H0 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 0 := ⟨4,0,1,rfl⟩
lemma H1 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 1 := ⟨0,5,0,rfl⟩
lemma H2 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 2 := ⟨1,0,2,rfl⟩
lemma H3 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 3 := ⟨0,3,1,rfl⟩
lemma H4 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 4 := ⟨8,0,0,rfl⟩
lemma H5 : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + 5 := ⟨0,1,2,rfl⟩

example (r : ℕ) (Hr : r < 6) : ∃ (a b c : ℕ), 6 * a + 9 * b + 20 * c = 44 + r :=
begin
  nat_lt_cases Hr,
  exacts [H0, H1, H2, H3, H4, H5]
end
```

#### [Kenny Lau (Nov 24 2018 at 23:20)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/148293413):
@**Kevin Buzzard**

#### [Kenny Lau (Nov 24 2018 at 23:20)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/148293415):
@**Mario Carneiro** how can I improve this tactic?

#### [Kevin Buzzard (Nov 24 2018 at 23:23)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/148293479):
Aah, they appear in the right order this time :-) I guess you can just `exacts [<4,0,1,rfl>,<0,5,0,rfl>,...]`

#### [Kevin Buzzard (Nov 24 2018 at 23:24)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/148293527):
Someone will want you to do it for `Ha : r > 2` `Hb : r <= 6` at some point

#### [Kenny Lau (Nov 24 2018 at 23:27)](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/6%20cases/near/148293580):
great

